plugins {
    id "java"
    id "maven-publish"
    id "net.minecrell.plugin-yml.bukkit" version "0.6.1-SNAPSHOT"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = "me.totalfreedom"
version = "4.8"
description = "BukkitTelnet"

repositories {
    mavenCentral()
    maven {
        url = uri("https://jitpack.io")
        content {
            includeGroup("com.github.MilkBowl")
        }
    }
    maven {
        url = uri("https://papermc.io/repo/repository/maven-public/")
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT"
    annotationProcessor "org.projectlombok:lombok:1.18.24"
    library "org.projectlombok:lombok:1.18.24"
    library "org.json:json:20220320"
    compileOnly("com.github.MilkBowl:VaultAPI:1.7") {
        exclude group: "org.bukkit", module: "bukkit"
    }
    library "org.apache.logging.log4j:log4j-api:2.17.2"
    library "org.apache.logging.log4j:log4j-core:2.17.2"
}
tasks {
    jar {
        dependsOn(shadowJar)
    }
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

bukkit {
    name = "BukkitTelnet"
    main = "me.totalfreedom.bukkittelnet.BukkitTelnet"
    apiVersion = "1.18"
    version = project.version
    description = "Telnet console access plugin."
    authors = ["bekvon", "Madgeek1450", "Prozza", "Taahh", "Telesphoreo"]
    softDepend = ["Vault"]
}

shadowJar {
    archiveBaseName.set("BukkitTelnet")
    archiveVersion.set("")
    archiveClassifier.set("")
}